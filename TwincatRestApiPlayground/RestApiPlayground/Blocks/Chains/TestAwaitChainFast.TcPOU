<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="TestAwaitChainFast" Id="{1e788789-0b04-435d-92b6-ba1fb20819dd}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK TestAwaitChainFast
VAR
	nState: UDINT := 0;
	await: Await;

	fbClient: FB_IotHttpClient := (
		sHostName:='localhost',
		bKeepAlive:=TRUE,
		tConnectionTimeout:=T#10S,
		nHostPort:=8000
	);
	
	fbRequest: FB_IotHttpRequest;
	sEndpoint: STRING := '/hello-world';
	response: STRING(255);

	fbJson: FB_JsonDomParser;
	
	jsonDoc: SJsonValue;
	jsonMember: SJsonValue;
	isJsonMemberString: BOOL;
	
	RisingEdge: R_TRIG;
	
	bSend: BOOL;
	requestSent: BOOL;
	cycleCounter: INT := 0;
	lenResponse: INT := 0;
	
	// Awaitable Errors
	onRequestError: OnRequestError := (errorName:='Request error', onErrorStep:=500);
	onResponseError: OnRequestError := (errorName:='Response error', onErrorStep:=500);
	onJsonError: OnJsonError := (errorName:='Json error', onErrorStep:=500);
	
	//Callbacks
	findJsonMemberCallback: FindJsonMember := 
	(
		jsonDomParser:=fbJson, 
		jsonReference:=jsonDoc,
		memberReference:=jsonMember
	);
	
	getJsonStringCallback: GetJsonString := 
	(
		jsonDomParser:=fbJson,
		bufferReference:=response,
		memberReference:=jsonMember
	);
	
	isJsonMemberStringCallback: IsJsonMemberString := 
	(
		jsonDomParser:=fbJson,
		memberReference:=jsonMember,
		isStringReference:=isJsonMemberString
	);
	
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[RisingEdge(CLK:= bSend );
fbClient.Execute();


CASE nState OF
	
	
	000: // Check if client is configured before moving on to the next step
		
		await.continueIf(fbClient.bConfigured);		
	
	
	010: // Waiting for input to start the request process
		
		await.continueIf(RisingEdge.Q);
	
	
	020: // Send the request
	
		cycleCounter := 0;
		
		requestSent := fbRequest.SendRequest(
			sUri:=sEndpoint, 
			fbClient:=fbClient, 
			eRequestType:=ETcIotHttpRequestType.HTTP_Get, 
			0, // payload, 
			0, // payload size  
			0  // header
		);
					
		await.catchIf(fbRequest.bError,onRequestError, 'An error occured while sending the request')
			.continueIf(requestSent);
	
	030: // Check if request was successful
	
		await.catchIf(fbRequest.bError,onRequestError, 'An error occured after sending the request')
			.continueIf(fbRequest.nStatusCode = 200);
			
		cycleCounter := cycleCounter + 1;
	
	040: // Get the Json response 

		jsonDoc := fbRequest.GetJsonDomContent(fbJson);
		findJsonMemberCallback.memberToFind := 'message';
		lenResponse := len(response);
		await.raiseIf(jsonDoc = 0, onJsonError, 'jsonDoc is an invalid pointer')
			.call(findJsonMemberCallback)
			.raiseIf(jsonMember = 0, onJsonError, 'jsonMember is an invalid pointer')
			.call(isJsonMemberStringCallback)
			.raiseIf(NOT isJsonMemberString, onJsonError, 'The type of the message response is not String')
			.call(getJsonStringCallback)
			.evalue(len(response) > 0, 0, onResponseError, 'The length of the response is 0');
					
		
	500: // Error step

			
	
END_CASE
]]></ST>
    </Implementation>
    <Method Name="FB_init" Id="{ea7cfd6d-4273-4c6c-9d48-9f9e0fb4f882}">
      <Declaration><![CDATA[METHOD FB_init : BOOL
VAR_INPUT
	bInitRetains : BOOL; // if TRUE, the retain variables are initialized (warm start / cold start)
	bInCopyCode : BOOL;  // if TRUE, the instance afterwards gets moved into the copy code (online change)
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[await.setStepPointer := ADR(nState);
await.setIncreaseValue := 10;]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="TestAwaitChainFast">
      <LineId Id="390" Count="47" />
      <LineId Id="455" Count="0" />
      <LineId Id="438" Count="9" />
      <LineId Id="9" Count="0" />
    </LineIds>
    <LineIds Name="TestAwaitChainFast.FB_init">
      <LineId Id="8" Count="0" />
      <LineId Id="7" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>